options:
	logo: &5&lParty &8:&7
	pchat_logo: &5&lParty &8|| &5%player% &8:&7


command /party [<text>] [<text>]:
	trigger:
		if arg-1 is "help":
			send "&8&m             &8[ &5Party &8]&m             "
			send "<ttp:&7Invite a player to your party><sgt:/party invite>&d/party invite <player>"
			send "<ttp:&7Accept a party invite><sgt:/party accept>&d/party &8(&daccept &8| &djoin&8) &d<player>"
			send "<ttp:&7Leave a party><sgt:/party leave>&d/party leave"
			send "<ttp:&7Promote a player to party leader><sgt:/party promote>&d/party promote <player>"
			send "<ttp:&7Kick a player from your party><sgt:/party kick>&d/party kick <player>"
			send "<ttp:&7Enable, disable or send a message in party chat><sgt:/party chat>&d/party chat [message]"
			send "&8&m             &8[ &5Party &8]&m             "

		else if arg-1 is "invite":
			if arg-2 is set:
				if arg-2 parsed as a player is set:
					if arg-2 parsed as a player is not player:
						if {party.%arg-2 parsed as a player%.owner} is player:
							send "{@logo} &d%arg-2 parsed as a player% &7is already in your party."
						else if {party.invited.%player%::%arg-2 parsed as a player%} is set:
							send "{@logo} You have already invited &d%arg-2 parsed as a player% &7to your party."
						else:
							set {party.invited.%player%::%arg-2 parsed as a player%} to true
							set {party.invited.%arg-2 parsed as a player%::%player%} to true
							send "{@logo} You have invited &d%arg-2 parsed as a player% &7to join your party."
							send "{@logo} You have been invited by &d%player% &7to join their party." to arg-2 parsed as a player
							wait 5 minutes
							if {party.invited.%player%::%arg-2 parsed as a player%} is true:
								delete {party.invited.%player%::%arg-2 parsed as a player%}
								delete {party.invited.%arg-2 parsed as a player%::%player%}
								send "{@logo} Your party invite to &d%arg-2 parsed as a player% &7has expired."

					else:
						send "{@logo} You can't invite yourself to your party."
				else:
					send "{@logo} The player &d%arg-2% &7is not found."
			else:
				send "{@logo} You have to give up a player to invite."


		else if arg-1 is "accept" or "join":
			if arg-2 is set:
				if arg-2 parsed as a player is set:
					if {party.%player%.owner} is set:
						send "{@logo} You first have to leave your party to join another one."
					else if {party.invited.%player%::%arg-2 parsed as a player%} is true:
						set {party.%player%.owner} to arg-2 parsed as a player
						if {party.%arg-2 parsed as a player%.owner} is not set:
							set {party.%arg-2 parsed as a player%.owner} to arg-2 parsed as a player
							add arg-2 parsed as a player to {party.%arg-2 parsed as a player%::*}
						add player to {party.%arg-2 parsed as a player%::*}
						delete {party.invited.%player%::%arg-2 parsed as a player%}
						delete {party.invited.%arg-2 parsed as a player%::%player%}
						send "{@logo} &d%player% &7has joined the party!" to {party.%arg-2 parsed as a player%::*}

					else:
						send "{@logo} You haven't been invited by &d%arg-2 parsed as a player%&7."
				else:
					send "{@logo} The player &d%arg-2% &7is not found."
			else:
				send "{@logo} You have to give up a player to accept their party invite."


		else if arg-1 is "leave":
			if {party.%player%.owner} is set:
				if {party.%player%.owner} is player:
					if size of {party.%player%::*} > 2:
						send "{@logo} You can't leave your party as the party owner, you must promote somebody else to the party owner."
						stop

				remove player from {party.%{party.%player%.owner}%::*}

				send "{@logo} You have left the party."
				send "{@logo} &d%player% &7has left the party." to {party.%{party.%player%.owner}%::*}
				delete {party.%player%.owner}
				delete {party.%player%.chat}
			else:
				send "{@logo} You aren't in a party."

		else if arg-1 is "promote":
			if {party.%player%.owner} is set:
				if {party.%player%.owner} is player:
					if arg-2 is not set:
						send "{@logo} Incorrect Usage! &d/party promote <player>&7."
					else if arg-2 parsed as a player is not set:
						send "{@logo} The player %arg-2% &7is not found."
					else if arg-2 parsed as a player is player:
						send "{@logo} You can't promote yourself to party leader."
					else if {party.%player%::*} doesn't contain arg-2 parsed as a player:
						send "{@logo} &d%arg-2 parsed as a player% &7is not in your party."
					else:
						set {_old} to {party.%player%.owner}
						set {_oldPlayers::*} to {party.%{_old}%::*}
						delete {party.%{_old}%::*}
						loop {_oldPlayers::*}:
							add loop-value to {party.%arg-2 parsed as a player%::*}
							set {party.%loop-value%.owner} to arg-2 parsed as a player

						send "{@logo} &d%arg-2 parsed as a player% &7has been promoted to party leader." to {party.%{party.%player%.owner}%::*}
				else:
					send "{@logo} You must be the party leader to do this."
			else:
				send "{@logo} You aren't in a party."

		else if arg-1 is "kick":
			if {party.%player%.owner} is set:
				if {party.%player%.owner} is player:
					if arg-2 is not set:
						send "{@logo} Incorrect Usage! &d/party kick <player>&7."
					else if arg-2 parsed as a player is not set:
						send "{@logo} The player %arg-2% &7is not found."
					else if arg-2 parsed as a player is player:
						send "{@logo} You can't promote yourself to party leader."
					else if {party.%player%::*} doesn't contain arg-2 parsed as a player:
						send "{@logo} &d%arg-2 parsed as a player% &7is not in your party."
					else:
						remove arg-2 parsed as a player from {party.%player%::*}
						delete {party.%arg-2 parsed as a player%.owner}
						delete {party.%player%.chat}
						send "{@logo} &d%arg-2 parsed as a player% &7has been kicked from the party." to {party.%player%::*}
						send "{@logo} You have been kicked from the party." to arg-2 parsed as a player
				else:
					send "{@logo} You must be the party leader to do this."
			else:
				send "{@logo} You aren't in a party."

		else if arg-1 is "chat":
			if {party.%player%.owner} is set:
				if arg-2 is set:
					send "{@pchat_logo} %arg-2%" to {party.%{party.%player%.owner}%::*}

				else:
					if {party.%player%.chat} is true:
						delete {party.%player%.chat}
						send "{@logo} Your party chat has been &ddisabled&7."
						play "UI_BUTTON_CLICK" to player at volume 10

					else:
						set {party.%player%.chat} to true
						send "{@logo} Your party chat has been &denabled&7."
						play "UI_BUTTON_CLICK" to player at volume 10

			else:
				send "{@logo} You aren't in a party."
		else:
			send "{@logo} Do &d/party help &7for more info."

on chat:
	if {party.%player%.chat} is true:
		cancel event
		send "{@pchat_logo} %message%" to {party.%{party.%player%.owner}%::*}

on quit:
	if {party.%player%.owner} is set:
		if {party.%player%.owner} is player:
			loop {party.%player%::*}:
				delete {party.%player%.owner}
				send "{@logo} The party has been disbanded as the party owner left the server." to loop-value
			clear {party.%player%::*}

		clear {party.%{party.%player%.owner}%::*}
		delete {party.%player%.owner}
		delete {party.%player%.chat}

	loop {party.invited.%player%::*}:
		delete {party.invited.%loop-value%::%player%}
		send {party.invited.%loop-value%::%player%} to console

	clear {party.invited.%player%::*}
